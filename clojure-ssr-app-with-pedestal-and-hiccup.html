<!DOCTYPE html>
<html lang="pt-br">
<head>
    <!-- Static Metadata -->
    <meta charset="UTF-8"/>
    <meta name="viewport" content="width=device-width, initial-scale=1.0"/>
    <link rel="icon" href="data:"/>
    <meta property="og:type" content="article"/>
    <meta name="twitter:card" content="summary"/>
    <!-- Metadata about me -->
    <meta name="twitter:site" content="@souenzzo"/>
    <meta name="twitter:creator" content="@souenzzo"/>
    <meta name="author" content="Enzzo Cavallo">
    <!-- Title metadata -->
    <title>Creating a Clojure project</title>
    <meta name="twitter:title" content="Clojure ssr app with Pedestal and Hiccup"/>
    <meta property="og:title" content="Clojure ssr app with Pedestal and Hiccup"/>
    <!-- Description metadata -->
    <meta name="twitter:description"
          content="Clojure ssr app with Pedestal and Hiccup"/>
    <meta name="Description"
          content="Clojure ssr app with Pedestal and Hiccup"/>
    <meta name="description"
          content="Clojure ssr app with Pedestal and Hiccup">
    <meta property="og:description"
          content="Clojure ssr app with Pedestal and Hiccup"/>
    <!-- https://jrl.ninja/etc/1 -->
    <style>
        section {
          max-width: 38em;
          padding: 1em;
          margin: auto;
        }
    </style>
</head>
<body>
<pre>
    # Clojure ssr app with Pedestal and Hiccup

    # Project setup
    In an empty dir, create a `deps.edn` with these content:
    ```
{:paths ["src" "resources"]
 :deps  {org.clojure/clojure          {:mvn/version "1.10.1"}
         io.pedestal/pedestal.service {:mvn/version "0.5.8"}
         io.pedestal/pedestal.jetty   {:mvn/version "0.5.8"}
         hiccup/hiccup                {:mvn/version "2.0.0-alpha2"}}}
    ```
    Let's understand these names:
    - :paths: A list of dirs that will be availble on the JVM classpath. Read more about that here: ()
    - :deps: A map of dependencies and it's versions
    - org.clojure/clojure: the clojure that we will use
    - io.pedestal/pedestal.service: the pedestal core
    - io.pedestal/pedestal.jetty: the adaptor that expose the pedestal core into a jetty http interface
    - hiccup/hiccup: an cool way to generate HTML in clojure

    # Start the REPL

    At this moment, we can start the REPL. Just type `clj` or use your editor to start one.

    Then create recursively the dirs `src/clj_ssr_app`, then the file `src/clj_ssr_app/main.clj`

    Let's write this `main.clj`

    ```
    (ns clj-ssr-app.main) ;; << this should match `clj_ssr_app/main.clj` inside `src` or `resources`

    (defn dev-start
      [& _]
      (prn :hello-world))
    ```

    Now let's test it on REPL

    ```
    (require 'clj-ssr-app.main :reload)
    nil
    (clj-ssr-app.main/dev-start)
    :hello-world
    nil
    ```
    Ok, everything working.


    # Setup pedestal

    ```
    (ns clj-ssr-app.main
      (:require [io.pedestal.http :as http])

    ;; our first handler
    (defn hello
      [req]
      {:body "Hello world!"
       :status 200})

    ;; our route table
    ;; each route is an array with the fileds
    ;; - the path
    ;; - the method
    ;; - the handler function
    ;; - the keyword :route-name (we will se more about this in a moment)
    ;; - a unique name for this route
    (def routes
      #{["/" :get hello :route-name :hello]})
    (defonce http-state (atom nil))
    (defn dev-start
      [& _]
      (swap! http-state (fn [st]
                          ;; if there is something running, stop it
                          (some-> st http/stop)
                          (-> {::http/routes routes
                               ::http/port   8080
                               ::http/join?  false
                               ::http/type   :jetty}
                              http/default-interceptors
                              http/dev-interceptors
                              http/create-server
                              http/start))))
    ```
    Now back on our repl

    ```
    ;; reload your changes
    (require 'clj-ssr-app.main :reload)
    nil
    ;; call start again
    (clj-ssr-app.main/dev-start)
    ```

    Now, if you connect your browser on localhost:8080 your should see a "hello world" message.

    # HTML response

    Let's work on make `hello` function into a HTML response

    ```
    (ns clj-ssr-app.main
      (:require [io.pedestal.http :as http]
                ;; add hiccup
                [hiccup2.core :as h])

    (defn hello
      [req]
      {:body   (->> [:html
                      [:head
                        [:title "Hello world!"]]
                      [:body
                        [:p {:style {:background-color "lightgeen"}} "Hello from HTML"]]]
                    ;; hiccup2 by default generates xhtml.
                    (h/html {:mode :html})
                    ;; we need to manually prepend this to make the document valid
                    (str "<!DOCTYPE html>\n"))
       ;; we need this headers to talk to the browser that it's a HTML response
       :headers {"Content-Type" "text/html"}
       :status 200})
    ```

    Ok, now back on REPL

    ```
    ;; reload your changes
    (require 'clj-ssr-app.main :reload)
    nil
    ;; restart the HTTP server (we will have hot-reload in a near future)
    (clj-ssr-app.main/dev-start)
    ```

    Then connect your browser into `https://localhost:8080`


    # Interceptors

    Pedestal has the concept of "interceptor", that is similar to "middlewares": help you to transform requests/responses.

    Let's create a interceptor that handles HTML

    ```
(def html-response
  {:name  ::html-response
   :leave (fn [{:keys [response]
                :as   ctx}]
            (if (contains? response :html)
              (let [html-body (->> response
                                   :html
                                   (h/html {:mode :html})
                                   (str "<!DOCTYPE html>\n"))]
                (assoc ctx :response (-> response
                                         (assoc :body html-body)
                                         (assoc-in [:headers "Content-Type"] "text/html"))))
              ctx))})
    ```
   And add it to our route
    ```
    ;; the 3nth element can be:
    ;; A- A function (let's call it handler) that receive a request and return a response
    ;; B- An interceptor
    ;; C- An array of interceptors, that may end on a handler function
    (def routes
      #{["/" :get [html-response hello] :route-name :hello]})
    ```

    Now we can simplify our hello handler

    ```
    (defn hello
      [req]
      {:html [:html
               [:head
                 [:title "Hello world!"]]
               [:body
                [:p {:style {:background-color "lightgeen"}} "Hello from HTML"]]]
       :status 200})
    ```

    After call that `require/reload/dev-main` stuff, you should see no difference.z

    # Rotes

    Let's add a new route and links between them
    ```
    ;; add [io.pedestal.route :as route] to your namespace

    (defn hello
      [req]
      {:html [:html
               [:head
                 [:title "Hello world!"]]
               [:body
                [:p {:style {:background-color "lightgeen"}} "Hello from HTML in green"]
                ;; let's add a link to yellow page here
                [:a {:href (route/url-for :hello-yellow)} "go to yellow"]]]
       :status 200})

    ;; Create this new handler
    (defn hello-yellow
      [req]
      {:html [:html
               [:head
                 [:title "Hello world!"]]
               [:body
                [:p {:style {:background-color "lightyellow"}} "Hello from HTML in yellow"]
                [:a {:href (route/url-for :hello)} "go to green"]]]
       :status 200})
    (def routes
      #{["/" :get [html-response hello] :route-name :hello]
        ;; and add to your routes
        ["/yellow" :get [html-response hello-yellow] :route-name :hello-yellow]})
    ```

</pre>
<footer>
    <p>
        In case of doubts or errors,
        <a target="_blank"
           rel="noreferrer noopener"
           href="https://github.com/souenzzo/souenzzo.github.io/issues/new">report an issue</a>
    </p>
    <p>If you prefer <a target="_blank"
                        rel="noreferrer noopener"
                        href="https://t.me/souenzzo">talk directly with me</a></p>
</footer>
</body>
</html>
